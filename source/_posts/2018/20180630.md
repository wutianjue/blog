---
title: Jvm规范学习之源码编译
date: 2018-06-30 09:52:39
tags:
- Jvm
---
The Java Virtual Machine Specification简称Jvm规范是由官方推出介绍Java虚拟机的文档，作为官方Jvm文档在国内也有不少分析介绍，但是遗憾是在国内相互转载，信息大多有偏差，而其他人学习理解也可能会扩大这种偏差。于是我翻阅了官方的[Java 8虚拟机规范](https://docs.oracle.com/javase/specs/jvms/se8/html/index.html)，借着谷歌翻译学习了下，下面分享一下。
<!-- more -->

## Jvm规范内容

在官方的[Java 8虚拟机规范序言](https://docs.oracle.com/javase/specs/jvms/se8/html/jvms-1.html)中介绍了整个规范分为七章，集合这七颗龙珠之后将会获得Jvm的神奇力量。

* [第一章](https://docs.oracle.com/javase/specs/jvms/se8/html/jvms-1.html)：序言，介绍Jvm历史和概括所讲内容
* [第二章](https://docs.oracle.com/javase/specs/jvms/se8/html/jvms-2.html)：概述了Jvm体系结构
* [第三章](https://docs.oracle.com/javase/specs/jvms/se8/html/jvms-3.html)：介绍Java源码如何转换为Jvm指令集
* [第四章](https://docs.oracle.com/javase/specs/jvms/se8/html/jvms-4.html)：描述了Class文件的格式
* [第五章](https://docs.oracle.com/javase/specs/jvms/se8/html/jvms-5.html)：描述了Jvm的启动以及类和接口的加载，链接和初始化
* [第六章](https://docs.oracle.com/javase/specs/jvms/se8/html/jvms-6.html)：按字母顺序详细介绍了每个Jvm指令
* [第七章](https://docs.oracle.com/javase/specs/jvms/se8/html/jvms-7.html)：以表格的形式展示所有的Jvm指令

第二章是整个Jvm规范中最为重要的部分，但是我的建议是第二章先略读，在学习了解三四五章节之后再精读第二章节可以达到最佳的理解效果。六七章节类似于速查手册，因此了解即可。在下一部分我将会来介绍Jvm第三章的内容：Java源码编译为Jvm指令。

## Jvm指令的基本格式

在第三章开篇介绍了Java含两种编译器：

* 源码编译器：Java源码转换为Jvm可运行的class字节码文件，class文件格式将会在第四章详细介绍
* 即时编译器：Jvm运行过程中实时将Jvm指令翻译为特定CPU的运行指令

第三章中即时编译只是略提，重点阐述源码到Jvm指令的转换，本文也是以此展开。使用JDK自带的工具javap可以实现对class文件的查看，一般可以通过javap -c <class文件名> 或 javap -v <class文件名>获取源码编译后的内容，具体内容格式在第四章。此处只解读在方法域中得到jvm指令。指令格式：

```bash
<index> <opcode> [ <operand1> [ <operand2>... ]] [<comment>]
```

可以在方法的Code区域中得到类似以上格式，index 表示在方法域中的索引值或是偏移量。opcode代表处理指令，根据第七章的表格分类可分为：Constants、Loads、Stores、Stack、Math、Conversions、Comparisons、References、Control、Extended以及Reserved。comment表示注释，一部分由javap工具生成，其余为编程人员编写。

## 从指令看基础操作

Jvm是面向栈操作的，因此会经常用到指令表中的Loads、Stores、Stack这类操作进行出栈入栈，Math、Conversions、Comparisons这类指令进行栈值的运算、比较或是转换。这里整合了第三章2至5小结的内容，包含数据常量、局部变量、控制结构、算术处理、以及常量池访问。Java源码

```java
public static void test() {
    long a = 100L;
    int i;
    for (i = 0; i < 100; i++) {
    }
}
```

对应的jvm指令

```bash
public static void test();
  Code:
     0: ldc2_w        #24                 // long 100l
     3: lstore_0
     4: iconst_0
     5: istore_2
     6: goto          12
     9: iinc          2, 1
    12: iload_2
    13: bipush        100
    15: if_icmplt     9
    18: return
    LocalVariableTable:
      Start  Length  Slot  Name   Signature
          4      15     0     a   J
          6      13     2     i   I
```

下面我们由上面的例子来分析，LocalVariableTable方法的局部变量表，Slot表示位置istore_2、iload_2中的2对应的即局部变量表中的i，类似的lstore_0中的0即为a。但iconst_0表示数据常量0，意为将0压入栈与bipush 0等价，不过iconst_0更加高效简洁，可以有效降低class文件的大小。而istore_2则是将栈内是数据取出存储至局部变量i，goto至第12处，此时压入局部变量i进栈，之后压入100进栈，在之后if_icmplt比较压入栈的i与100进行比较，若是i<100则继续进如第9处，此处iinc表示对序号为2的局部变量也就是i进行加1操作，之后继续压i和100入栈比较，直到i等于100的时候进入14处返回。可以看到在0处有#24，这就是代表访问常量池中的序号24的值，此处有注释这个#24表示的是100 long类型。

## 从指令看方法调用

这部分包含第三章的6、7小结内容，将会通过一个例子来介绍参数的传递以及方法的调用。Java源码

```java
public static int add(int a, int b) {
    return a + b;
}
public int add1(Integer a, int b) {
    add(a, b);
    return a + b;
}
```

对应的jvm指令

```bash
public static int add(int, int);
  Code:
       0: iload_0
       1: iload_1
       2: iadd
       3: ireturn
    LocalVariableTable:
      Start  Length  Slot  Name   Signature
          0       4     0     a   I
          0       4     1     b   I
public int add1(java.lang.Integer, int);
  Code:
       0: aload_1
       1: invokevirtual #25                 // Method java/lang/Integer.intValue:()I
       4: iload_2
       5: invokestatic  #31                 // Method add:(II)I
       8: pop
       9: aload_1
      10: invokevirtual #25                 // Method java/lang/Integer.intValue:()I
      13: iload_2
      14: iadd
      15: ireturn
    LocalVariableTable:
      Start  Length  Slot  Name   Signature
          0      16     0  this   Lcom/toss/vertx/Test;
          0      16     1     a   Ljava/lang/Integer;
          0      16     2     b   I
```

这里有两个不同的方法，一是静态方法，一是类型实例化后调用。add这个静态方法，在索引0、1处将参数的值压入栈，经过iadd计算得出结果保留在栈中，再由ireturn弹出结果。再看add1这个方法，注意：所有的实例方法的第一个局部参数永远是实例本身，不像静态方法那样局部变量后缀是从0开始，而是由1开始。add方法参数均为基本int类型，而add1中a则是包装类型Integer，所以索引1处调用了Integer的intValue将其拆箱为int类型，然后压入b进栈，调用add方法得到结果，之后由于没有变量接收结果，因此直接pop出结果清空栈。最后的a + b也是如此，Integer类型与int类型相加也要转为int类型。这里可以看到#25、#31，这些代表Jvm在处理时访问了常量池。#25代表了实例调用了intValue方法；#31则代表了静态方法add。

## 从指令看引用类型

对象、数组都是引用类型。那么这些引用类型是如何初始化的。创建引用类型

```java
public void newObject() {
    Object object = new Object();
    Object[] objects = new Object[10];
    objects[0] = object;
    int[] ints = new int[10];
    ints[0] = 1;
}
```

对应的jvm指令

```bash
public void newObject();
  Code:
       0: new           #3                  // class java/lang/Object
       3: dup
       4: invokespecial #8                  // Method java/lang/Object."<init>":()V
       7: astore_1
       8: bipush        10
      10: anewarray     #3                  // class java/lang/Object
      13: astore_2
      14: aload_2
      15: iconst_0
      16: aload_1
      17: aastore
      18: bipush        10
      20: newarray       int
      22: astore_3
      23: aload_3
      24: iconst_0
      25: iconst_1
      26: iastore
      27: return
    LocalVariableTable:
      Start  Length  Slot  Name   Signature
          0      28     0  this   Lcom/toss/vertx/Test;
          8      20     1 object   Ljava/lang/Object;
         14      14     2 objects   [Ljava/lang/Object;
         23       5     3  ints   [I
```

先看看单个对象的初始化，其中的new引用了常量池的类，dup将class入栈，invokespecial意为调用了相关的特殊方法此处为构造方法，对于任意类的构造方法其方法在调用时隐含名都是\<init>，最后出栈保存在局部变量1中也就是变量名为object。而对于对象类型的数组构造时先将数组的大小入栈，再将对象的类型入栈组装，最后将数组类型出栈保存在objects中。那么数组是如何操作的？指令14至17行先将数组入栈再将要操作的数组下标入栈，在将对应要保存的数组元素入栈，最后保存完毕。

## 从指令看异常处理

异常处理

```java
public Object exception() {
    Object result = null;
    try {
        result = new Object();
    } catch (IllegalArgumentException e) {
        e.printStackTrace();
    } finally {
        System.out.println();
    }
    return result;
}
```

对应的jvm

```bash
public java.lang.Object exception();
  Code:
       0: aconst_null
       1: astore_1
       2: new           #3                  // class java/lang/Object
       5: dup
       6: invokespecial #8                  // Method java/lang/Object."<init>":()V
       9: astore_1
      10: goto          36
      13: astore_2
      14: aload_2
      15: invokevirtual #21                 // Method java/lang/IllegalArgumentException.printStackTrace:()V
      18: getstatic     #26                 // Field java/lang/System.out:Ljava/io/PrintStream;
      21: invokevirtual #32                 // Method java/io/PrintStream.println:()V
      24: goto          42
      27: astore_3
      28: getstatic     #26                 // Field java/lang/System.out:Ljava/io/PrintStream;
      31: invokevirtual #32                 // Method java/io/PrintStream.println:()V
      34: aload_3
      35: athrow
      36: getstatic     #26                 // Field java/lang/System.out:Ljava/io/PrintStream;
      39: invokevirtual #32                 // Method java/io/PrintStream.println:()V
      42: aload_1
      43: areturn
    Exception table:
       from    to  target type
           2    10    13   Class java/lang/IllegalArgumentException
           2    18    27   any
    LocalVariableTable:
      Start  Length  Slot  Name   Signature
          0      44     0  this   Lcom/toss/vertx/Test;
          2      42     1 result   Ljava/lang/Object;
         14       4     2     e   Ljava/lang/IllegalArgumentException;
```

这个例子中可以看到此处对应的三条不同执行路线。正常执行0-10，36-43 。IllegalArgumentException异常捕获2-10，异常处理13-24，结果返回42-43 。未知异常捕获2-18，异常处理27-35。这在里会看到多了一个Exception table，这个异常表记录了当前函数有可能出现的异常。from to 表示异常的监控指令区域，每当异常出现时都会有一个局部变量来储存，位于13的IllegalArgumentException异常以及位于27的any异常。可以看到在未知异常的处理时最后时将异常读取抛出。

## 从指令看synchronized

```java
public synchronized void exception(Test test) {
    synchronized (test) {
        System.out.println();
    }
}
```

```java
public synchronized void exception(com.toss.vertx.Test);
  flags: ACC_PUBLIC, ACC_SYNCHRONIZED
  Code:
    stack=2, locals=3, args_size=2
       0: aload_1
       1: dup
       2: astore_2
       3: monitorenter
       4: getstatic     #24                 // Field java/lang/System.out:Ljava/io/PrintStream;
       7: invokevirtual #30                 // Method java/io/PrintStream.println:()V
      10: aload_2
      11: monitorexit
      12: goto          18
      15: aload_2
      16: monitorexit
      17: athrow
      18: return
    Exception table:
       from    to  target type
           4    12    15   any
          15    17    15   any
    LocalVariableTable:
      Start  Length  Slot  Name   Signature
          0      19     0  this   Lcom/toss/vertx/Test;
          0      19     1  test   Lcom/toss/vertx/Test;
```

同步关键字的处理：同步块是由monitorenter和monitorexit指令显示的处理，而用synchronized修饰的方法则会在flags中加上ACC_SYNCHRONIZED来进行隐式处理。这两个指令的意为监控的进入以及监控的退出。其中无论结果是正常退出还是异常退出，可以看到在同步块中出现了两处异常捕获均是为了确保monitorenter和monitorexit成对的出现。

## 写在最后

这篇文章我拖了很久，我希望都能覆盖到但是后来发现不现实，只能算是给需要的人带来一些启发，希望真正有兴趣的人可以直接前往官网学习，相信必定会收获不少。
